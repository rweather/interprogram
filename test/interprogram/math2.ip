TITLE Math test 2 - Trigonometric functions

# Classic INTERPROGRAM expresses angles to sin, cos, etc, in units of pi.
# For example, 0.25 is 45 degrees, 1.0 is 180 degrees, etc.
# Extended INTERPROGRAM uses radians instead.

# sin(2 radians) = 0.9092974268...
take 2
form sine
subtract 0.9092974268
form absolute value
if this is greater than 0.000001, go to FAIL

# sin(sin(2 radians) = 0.78907234357...
# Tests repetition of a built-in library function using "&".
take 2
form sine, &
subtract 0.78907234357
form absolute value
if this is greater than 0.000001, go to FAIL

# sin(3 radians) = 0.141120008...
take absolute value of (sine of 3 - 0.141120008)
if this is greater than 0.000001, go to FAIL

# cos(5 radians) = 0.283662185...
take 5
form cosine
subtract 0.283662185
form absolute value
if this is greater than 0.000001, go to FAIL

# cos(1.5 radians) = 0.0707372016...
take absolute value of (cosine of 1.5 - 0.0707372016)
if this is greater than 0.000001, go to FAIL

# tan(0.5 radians) = 0.5463024898...
take 0.5
form tangent
subtract 0.5463024898
form absolute value
if this is greater than 0.000001, go to FAIL

# tan(1.5 radians) = 14.101419947...
take absolute value of (tangent of 1.5 - 14.101419947)
if this is greater than 0.000001, go to FAIL

# atan(0.5463024898) = 0.5
take 0.5463024898
form arctan
subtract 0.5
form absolute value
if this is greater than 0.000001, go to FAIL

# atan(14.101419947) = 1.5
take absolute value of (arctan of 14.101419947 - 1.5)
if this is greater than 0.000001, go to FAIL

# If we get here, then all tests have passed.
end of interprogram

# One of the tests failed.  Exit the program with a status of 1.
*FAIL
take 1, exit interprogram
