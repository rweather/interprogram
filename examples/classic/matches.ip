(1) TITLE GAME OF 21 MATCHES
(2) SYMBOLS FOR INTEGERS MATCHES, COUNT, ANSWER
(4) COMPILE THE FOLLOWING INTERPROGRAM

# Note: This version of 21 Matches is unbeatable because the computer cheats.
# See "extended/matches.ip" for a fairer version.

*1 PUNCH THE FOLLOWING CHARACTERS,
GAME OF 21 MATCHES

WE START WITH 21 MATCHES AND TAKE TURNS REMOVING 1, 2, OR 3 MATCHES.
THE PERSON TO HAVE TO TAKE THE LAST MATCH LOSES.  YOU GO FIRST.
~~~~~

    SET MATCHES = 21
*2  PUNCH THE FOLLOWING CHARACTERS,

NUMBER OF MATCHES LEFT: ~~~~~
    OUTPUT MATCHES

    # PLAYER'S TURN.
    PUNCH THE FOLLOWING CHARACTERS,
HOW MANY DO YOU TAKE? ~~~~~
    INPUT COUNT
    IF COUNT IS SMALLER THAN 1, GO TO *3
    IF COUNT IS GREATER THAN 3, GO TO *3

    # COMPUTER'S TURN.  ALWAYS TAKE "4 - COUNT" MATCHES.  THIS KEEPS THE
    # TOTAL A MULTIPLE OF 4 UNTIL THE PLAYER IS FORCED TO TAKE THE LAST MATCH.
    TAKE 4
    SUBTRACT COUNT
    PUNCH THE FOLLOWING CHARACTERS,
I TAKE ~~~~~
    OUTPUT THIS

    # IS THE GAME OVER YET?
    TAKE MATCHES
    SUBTRACT 4
    REPLACE MATCHES
    IF THIS IS GREATER THAN 1, GO TO *2

    # COMPUTER HAS WON!
    PUNCH THE FOLLOWING CHARACTERS,
I WIN!  WANT ANOTHER GAME? (1 FOR YES, OR 0 FOR NO) ~~~~~
    INPUT ANSWER
    PUNCH THE FOLLOWING CHARACTERS,

~~~~~
    IF ANSWER IS GREATER THAN 0, GO TO *1
    PUNCH THE FOLLOWING CHARACTERS,
OH WELL, IT HAS BEEN NICE PLAYING YOU!
~~~~~
    GO TO *4

    # INVALID INPUT.
*3  PUNCH THE FOLLOWING CHARACTERS,
EITHER 1, 2, OR 3 PLEASE!
~~~~~
    GO TO *2

*4  END OF INTERPROGRAM
