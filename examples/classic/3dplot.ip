# PORTED FROM THE "3-D PLOT" PROGRAM FROM "BASIC COMPUTER GAMES",
# CREATIVE COMPUTING PRESS, 1978.
#
# THIS VERSION IS WRITTEN IN THE CLASSIC INTERPROGRAM SYNTAX.

(1) TITLE 3D PLOT
(2) SYMBOLS FOR INTEGERS L, Z, YEXTENT
(4) COMPILE THE FOLLOWING INTERPROGRAM

    # OUTER LOOP OVER X.
    SET X = -30
*1
    # LAST COLUMN THAT WAS USED TO DISPLAY A POINT.
    SET L = 0

    # CALCULATE THE EXTENT OF THE Y RANGE.
    TAKE X
    MULTIPLY BY THIS, & -1
    ADD 900
    FORM SQUARE ROOT
    DIVIDE BY 5
    REPLACE YEXTENT     # CONVERT TO INTEGER.
    TAKE YEXTENT
    MULTIPLY BY 5
    REPLACE YEXTENT

    # INNER LOOP OVER Y.
    SET Y = YEXTENT
    SET YEXTENT = -YEXTENT
*2
    # DETERMINE THE COLUMN THAT SHOULD CONTAIN THE NEXT POINT.
    TAKE X
    MULTIPLY BY THIS
    REPLACE TEMP
    TAKE Y
    MULTIPLY BY THIS
    ADD TEMP
    FORM SQUARE ROOT
    EXECUTE PROCESS *100
    ADD 25
    REPLACE TEMP
    TAKE 0.7
    MULTIPLY BY Y
    REPLACE TEMPTWO
    TAKE TEMP
    SUBTRACT TEMPTWO
    REPLACE Z

    # PAD WITH SPACES OVER TO THE REQUIRED COLUMN AND DISPLAY THE POINT.
    TAKE L
    SUBTRACT Z
    IF THIS IS POSITIVE, GO TO *4
*3
    PUNCH THE FOLLOWING CHARACTERS
 ~~~~~
    SET L = L + 1
    IF L IS SMALLER THAN Z, GO TO *3
    PUNCH THE FOLLOWING CHARACTERS
*~~~~~

    # MOVE ON TO THE NEXT Y VALUE.
*4
    SET Y = Y - 5
    IF Y IS SMALLER THAN YEXTENT, GO TO *5
    GO TO *2
*5

    # END THE CURRENT LINE.
    PUNCH THE FOLLOWING CHARACTERS

~~~~~

    # MOVE ON TO THE NEXT X VALUE.
    SET X = X + 1.5
    IF X IS SMALLER THAN 30.1, GO TO *1
    GO TO *200

# DEFINITION OF THE FUNCTION A(Z) = 30 * EXP(-Z*Z/100)
*100
    MULTIPLY BY THIS
    DIVIDE BY -100
    FORM EXPONENTIAL
    MULTIPLY BY 30
    END OF PROCESS DEFINITION

# END OF THE PROGRAM.
*200
    END OF INTERPROGRAM
